plugins {
    id 'java-library'
    id "io.freefair.lombok" version "${ioFreeFairLombok}"
    id 'maven-publish'
}

repositories {
	mavenLocal()
	maven {
		url = "https://maven.pkg.github.com/Vempain/vempain-auth"
		credentials {
			username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
			password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
		}
	}
	mavenCentral()
}

group = 'fi.poltsi.vempain'
version = project.hasProperty("releaseVersion") ? project.releaseVersion : '0.0.1-SNAPSHOT'

jar {
	archiveFileName = "${rootProject.name}-api-${version}.jar"
}

java {
    // Attach sources and javadoc for Maven consumers
    withSourcesJar()
    withJavadocJar()
}

// Quiet javadoc warnings as we're using Lombok which does not generate documentation for the generated methods
tasks.withType(Javadoc).configureEach {
	options.addStringOption('Xdoclint:all,-missing', '-quiet') // keep other checks, ignore missing docs
}

dependencies {
	api group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: "${jacksonVersion}"
	api group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "${jacksonVersion}"
	api  group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: "${springBootVersion}"
	// https://mvnrepository.com/artifact/org.springframework.data/spring-data-jpa
	api group: 'org.springframework.data', name: 'spring-data-jpa', version: '3.5.3'
	// This needs to be built first in vempain-auth
	implementation group: 'fi.poltsi.vempain', name: 'vempain-auth-api', version: "${vempainAuthVersion}"
    // https://mvnrepository.com/artifact/io.swagger.core.v3/swagger-annotations
    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations', version: '2.2.36'
    // https://mvnrepository.com/artifact/jakarta.validation/jakarta.validation-api
    implementation group: 'jakarta.validation', name: 'jakarta.validation-api', version: '3.1.1'
	// https://mvnrepository.com/artifact/jakarta.persistence/jakarta.persistence-api
	implementation group: 'jakarta.persistence', name: 'jakarta.persistence-api', version: '3.1.0'
}

// Publish the API module to GitHub Packages when invoked by CI
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            // Optional: keep artifactId as project name ('api')
            groupId = project.group
            artifactId = "vempain-admin-backend-api"
            version = project.version
            pom {
                name = "vempain-admin-backend-api"
                description = "Public API (DTOs and interfaces) for Vempain Admin backend"
                url = "https://github.com/Vempain/vempain-admin-backend"
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }
                scm {
                    url = "https://github.com/Vempain/vempain-admin-backend"
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Vempain/vempain-admin-backend")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
