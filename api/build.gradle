plugins {
	id 'java-library'
	id "io.freefair.lombok" version "${ioFreeFairLombok}"
	id 'maven-publish'
	id "jacoco"
}

repositories {
	maven {
		url = "https://maven.pkg.github.com/Vempain/vempain-auth"
		credentials {
			username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
			password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
		}
	}
	mavenCentral()
}

group = 'fi.poltsi.vempain'
version = project.hasProperty("releaseVersion") ? project.releaseVersion : '0.0.1-SNAPSHOT'

jar {
	archiveFileName = "${rootProject.name}-api-${version}.jar"
}

java {
	// Attach sources and javadoc for Maven consumers
	withSourcesJar()
	withJavadocJar()
}

// Quiet javadoc warnings as we're using Lombok which does not generate documentation for the generated methods
tasks.withType(Javadoc).configureEach {
	options.addStringOption('Xdoclint:all,-missing', '-quiet') // keep other checks, ignore missing docs
}

dependencies {
	api "org.springframework.boot:spring-boot-jackson:${springBootVersion}"
	api "org.springframework.boot:spring-boot-starter-webmvc:${springBootVersion}"
	api "org.springframework.data:spring-data-jpa:${springBootVersion}"
	// This needs to be built first in vempain-auth
	api "fi.poltsi.vempain:vempain-auth-api:${vempainAuthVersion}"
	api "fi.poltsi.vempain:vempain-file-backend-api:${vempainFileVersion}"
	implementation "io.swagger.core.v3:swagger-annotations:${swaggerVersion}"
	// https://mvnrepository.com/artifact/jakarta.validation/jakarta.validation-api
	implementation "jakarta.validation:jakarta.validation-api:3.1.1"
	testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
	testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
	testImplementation "org.junit.platform:junit-platform-launcher:${junitVersion}"
}

// Publish the API module to GitHub Packages when invoked by CI
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			// Optional: keep artifactId as project name ('api')
			groupId = project.group
			artifactId = "vempain-admin-backend-api"
			version = project.version
			pom {
				name = "vempain-admin-backend-api"
				description = "Public API (DTOs and interfaces) for Vempain Admin backend"
				url = "https://github.com/Vempain/vempain-admin-backend"
				licenses {
					license {
						name = "GPL-2.0 License"
						url = "https://www.gnu.org/licenses/old-licenses/gpl-2.0.en.html"
					}
				}
				scm {
					url = "https://github.com/Vempain/vempain-admin-backend"
				}
			}
		}
	}
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/Vempain/vempain-admin-backend")
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
				password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
			}
		}
	}
}

jacoco {
	toolVersion = "0.8.13"
}

tasks.named('test') {
	useJUnitPlatform()
}

test {
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
	}
}
